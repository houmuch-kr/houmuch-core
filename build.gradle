plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.springframework.boot' version '2.7.4'
    id 'io.spring.dependency-management' version '1.1.0'
}

group = 'kr.co.houmuch'
version = '1.0.1-SNAPSHOT'
sourceCompatibility = '17'
ext.profile = (!project.hasProperty('profile') || !profile) ? 'local' : profile

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:2.7.4"
    }
}

java {
    withSourcesJar()
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}


publishing {
    repositories {
        profile.toString() == "local" ? mavenLocal() : maven {
            url = uri("https://maven.pkg.github.com/houmuch-kr/registry")
            credentials {
                username = System.getenv("GITHUB_USERNAME")
                password = System.getenv("GITHUB_PASSWORD")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            from components.java
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    implementation 'com.fasterxml.jackson.core:jackson-core'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    testImplementation 'com.h2database:h2'
}

tasks.named('publishToMavenLocal') {
    dependsOn 'test'
}

tasks.named('test') {
    useJUnitPlatform()
}

